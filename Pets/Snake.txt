
// Pet Snake
// by: standardcombo
// v1.2


var petColor
petColor = #white

var keepDistance = -30
var maxX = -14

var maxDistance
maxDistance = -70

var targetZ = -1

var x = 0
var y = 0
var myX = 0
var myZ = 0
var lastPlayerX = 0
var lastPlayerZ = 0

var stateNone = 0
var stateIdle = 1
var stateWalk = 2
var currentState = stateNone
var stateTime = 0


func SetState(newState)
  ?newState = stateWalk
    play serpent_slither

  currentState = newState
  stateTime = 0


?currentState = stateNone | time = 1
  lastPlayerX = pos.x
  lastPlayerZ = pos.z
  myX = keepDistance
  myZ = targetZ

  // Start further away if the pet is summoned while the location is already running
  ?time > 1
    myX = maxDistance

  SetState(stateIdle)


myX = myX - (pos.x - lastPlayerX)

stateTime++

?currentState = stateIdle
  ?myX < maxDistance | myX > -maxDistance
    myX = maxDistance
  myZ = myZ - (pos.z - lastPlayerZ)

  ?myX < keepDistance
    SetState(stateWalk)

:?currentState = stateWalk
  ?myX < maxX
    ?myX < keepDistance | time % 2 = 0
      myX++

  ?myZ > targetZ
    myZ--
  :?myZ < targetZ
    myZ++

  :?myX = maxX
    SetState(stateIdle)


lastPlayerX = pos.x
lastPlayerZ = pos.z

// Draw

?currentState = stateIdle
  >o@myX@,@myZ@,@petColor@,ascii
#########.-.
########((`-'
#########\\
#.'¯¯ -.__))
(´###`'---´
#`
asciiend
  ?face = "°°"
    ?time % 10 = 0 & myX > keepDistance
      myX--

    ?time % 4 <= 1
      >o@myX@,@myZ@,@petColor@,ascii
##########o°
asciiend
    ?time % 4 > 1
      >o@myX@,@myZ@,@petColor@,ascii
##########°o
asciiend

  :?stateTime % 30 <= 2
    >o@myX@,@myZ@,@petColor@,ascii
#
#############-
asciiend
  :?stateTime % 30 <= 4
    >o@myX@,@myZ@,@petColor@,ascii
#
#############-<
asciiend
  :?stateTime % 30 <= 6
    >o@myX@,@myZ@,@petColor@,ascii
#
#############-
asciiend

?currentState = stateWalk
  ?stateTime % 9 = 0
    >o@myX@,@myZ@,@petColor@,ascii
##########.-.
#########((`-'
##########\\
#.--._####_))
(´ `'.`¯¯.-´
###### ¯¯
asciiend

  :?stateTime % 9 = 1
    >o@myX@,@myZ@,@petColor@,ascii
##########.-.
#########((`-'
##########\\
.-.######__))
´ `:'--'¯.-´
#### `-'¯
asciiend

  :?stateTime % 9 = 2
    >o@myX@,@myZ@,@petColor@,ascii
##########.-.
#########((`-'
##########\\
`.#######__))
##·`---'¯.-´
### `'-'¯
asciiend

  :?stateTime % 9 = 3
    >o@myX@,@myZ@,@petColor@,ascii
##########.-.
#########((`-'
##########\\
#:######___))
#\'---'¯_.-´
###`'-'¯
asciiend

  :?stateTime % 9 = 4
    >o@myX@,@myZ@,@petColor@,ascii
##########.-.
#########((`-'
##########\\
##.###_,.__))
#('. ´.--.-´
##` ¯´
asciiend

  :?stateTime % 9 = 5
    >o@myX@,@myZ@,@petColor@,ascii
##########.-.
#########((`-'
##########\\
#####.--.__))
(·_.´.''-.-´
#`'-´
asciiend

  :?stateTime % 9 = 6
    >o@myX@,@myZ@,@petColor@,ascii
##########.-.
#########((`-'
##########\\
####.---.__))
__.´.' '-.-´
`'´
asciiend

  :?stateTime % 9 = 7
    >o@myX@,@myZ@,@petColor@,ascii
##########.-.
#########((`-'
##########\\
###.'¯'-.__))
#/.'´ `'-.-´
·´
asciiend

  :?stateTime % 9 = 8
    >o@myX@,@myZ@,@petColor@,ascii
##########.-.
#########((`-'
##########\\
##.'¯¯ -.__))
#(´###`'---´
##`
asciiend