// Pet Jack O Lantern
// by: michael.g.g.
// v1.1
// needs big head mode for eye expressions
// Puts a little Jack O' Lantern as a friend who follows you around
// Face flickers with candlelight 

var lightColor
var stemColor

//change colors of the pumpkin by changing the hex values below. can by from #000000 to #FFFFFF
lightColor = #yellow //eyes, mouth
stemColor = #706020 //little stem on top - brown by default

var keepDistance = -21 // distance at which the movement animation will play (stateMoving)
var maxX = -20         // closest offset pet will get to character.  Closer to 0, closer to character
var maxDistance = -35  // if the pet exceeds this distance, it will teleport to this distance (cannot be further away than this)
var targetZ = 2       //start the drawing 1 unit above the player

?loc.loop
  keepDistance = -21
  maxX = -12
  maxDistance = -35
  targetZ = 2

var x = 0
var y = 0
var myX = 0
var myZ = 0
var lastPlayerX = 0
var lastPlayerZ = 0

var stateNone = 0
var stateIdle = 1
var stateMoving = 2
var currentState = stateNone
var stateTime = 0

func SetState(newState)
  ?newState = stateMoving
    play haunted_gate_shuffle //play sound when moving
  
  currentState = newState
  stateTime = 0

?currentState = stateNone | time = 1
  lastPlayerX = pos.x
  lastPlayerZ = pos.z
  myX = keepDistance
  myZ = targetZ

// Start further away if pumpkin is summoned while the location is already running
  ?time > 1
    myX = maxDistance

  SetState(stateIdle)

myX = myX - (pos.x - lastPlayerX) // If player has moved, increment myX (distance from player)

stateTime++

//set flickering for eyes and mouth based on stateTime
?stateTime % 2 = 0
	lightColor = #yellow
:
	lightColor = #white

?currentState = stateIdle 
  ?myX < maxDistance | myX > -maxDistance
    myX = maxDistance
  myZ = myZ - (pos.z - lastPlayerZ)

  ?myX < keepDistance
    SetState(stateMoving)
	
:?currentState = stateMoving
  ?myX < maxX
    myX++
	
  ?stateTime >= 6
    ?myZ > targetZ
      myZ--
    :?myZ < targetZ
      myZ++

  ?stateTime >= 12
    SetState(stateIdle)
	
lastPlayerX = pos.x
lastPlayerZ = pos.z

//draw pumpkin-----------------------------
?currentState = stateIdle 
  >o@myX@,@myZ-3@,@#CC9020@,ascii
#,`^ .
({(Δ_Δ}
#``  ´ 
asciiend
  >o@myX@,@myZ-3@,@lightColor@,ascii
#
###Δ_Δ
# 
asciiend
  >o@myX@,@myZ-3@,@stemColor@,ascii
##`^
#
#
asciiend

//eye expressions
  ?face = "°°"
    >o@myX-1@,@myZ-4@,@lightColor@,ascii
#
#
####ʘ#ʘ
asciiend

  ?face = "^^"
    >o@myX-1@,@myZ-4@,@lightColor@,ascii
#
#
####^O^
asciiend

//Blink
?currentState = stateIdle
  ?face ! "^^"
    ?face ! "°°"
      ?stateTime % 50 >= 48
        >o@myX-1@,@myZ-4@,@lightColor@,ascii
#
#
####-#-
asciiend
      :?stateTime % 50 >= 46
        >o@myX-1@,@myZ-4@,@lightColor@,ascii
#
#
####-#-
asciiend
      :?stateTime % 50 >= 44
        >o@myX-1@,@myZ-4@,@lightColor@,ascii
#
#
####-#-
asciiend

//draw moving----------------------------
?currentState = stateMoving
  ?stateTime/3 = 0
   >o@myX@,@myZ-3@,@#CC9020@,ascii
#,`^ .
({(Δ_Δ}
#``  ´ 
asciiend
   >o@myX@,@myZ-3@,@lightColor@,ascii
#
###Δ_Δ
# 
asciiend
   >o@myX@,@myZ-3@,@stemColor@,ascii
##`^
#
#
asciiend

  ?stateTime/3 = 1
   >o@myX@,@myZ-3@,@#CC9020@,ascii
#.,^ .
{({|Δ_'
#``  ´ 
asciiend
   >o@myX@,@myZ-3@,@lightColor@,ascii
#
####Δ_
# 
asciiend
   >o@myX@,@myZ-3@,@stemColor@,ascii
###^
#
#
asciiend

  ?stateTime/3 = 2
   >o@myX@,@myZ-3@,@#CC9020@,ascii
#.,^'.
({(|)Δ'
#``  ´ 
asciiend
   >o@myX@,@myZ-3@,@lightColor@,ascii
#
#####^
# 
asciiend
   >o@myX@,@myZ-3@,@stemColor@,ascii
###^'
#
#
asciiend

  ?stateTime/3 = 3
   >o@myX@,@myZ-3@,@#CC9020@,ascii
#.,^ .
{({|Δ_'
#``  ´ 
asciiend
   >o@myX@,@myZ-3@,@lightColor@,ascii
#
####Δ_
# 
asciiend
   >o@myX@,@myZ-3@,@stemColor@,ascii
###^
#
#
asciiend

  ?stateTime/3 = 4
   >o@myX@,@myZ-3@,@#CC9020@,ascii
#,`^ .
({(Δ_Δ}
#``  ´ 
asciiend
   >o@myX@,@myZ-3@,@lightColor@,ascii
#
###Δ_Δ
# 
asciiend
   >o@myX@,@myZ-3@,@stemColor@,ascii
##`^
#
#
asciiend

  ?stateTime/3 = 5
    >o@myX@,@myZ-3@,@#CC9020@,ascii
#.#^ .
{{Δ_Δ}}
#`` ´´ 
asciiend
   >o@myX@,@myZ-3@,@lightColor@,ascii
#
##Δ_Δ
# 
asciiend
   >o@myX@,@myZ-3@,@stemColor@,ascii
###^
#
#
asciiend