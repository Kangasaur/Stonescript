/*
Metallophone
by ArtificialPotato
v0.5
Description: You can play musics with the mindstone!
***
At the Fairweather Faire, the stonehead heard strange sounds... might be a music.
As he approached, he saw that someone was hitting the remnants of something that had been placed somewhat regularly.
He decided to try striking it with his favorite sticks, too.
***
Instructions: import or copy&paste this script into your mindstone
then touch or click keys
*/

var key1=ascii
 ___________________________
|  | | | |  |  | | | | | |  |
|  | | | |  |  | | | | | |  |
|  | | | |  |  | | | | | |  |
|  |_| |_|  |  |_| |_| |_|  |
|   |   |   |   |   |   |   |
|   |   |   |   |   |   |   |
|___|___|___|___|___|___|___|
asciiend
var key2=ascii
____________________________
#  | | | |  |  | | | | | |  |
#  | | | |  |  | | | | | |  |
#  | | | |  |  | | | | | |  |
#  |_| |_|  |  |_| |_| |_|  |
#   |   |   |   |   |   |   |
#   |   |   |   |   |   |   |
#___|___|___|___|___|___|___|
asciiend
var wh=[0,2,4,5,7,9,11]
var bl=[1,3,6,8,10]

var y=0
var x=0
var isMobile=sys.isMobile
var played=-2
var w=screen.w
var h=screen.h
?played<0
    ?played=-2
        play ui_starold2
        played=-1
    ambient.Stop()
    music.Stop()
>`0,@h-10@,@key1@
?w>28*3
    >`28,@h-10@,@key2@
    >`56,@h-10@,@key2@
:?w>28*2
    >`28,@h-10@,@key2@
func pitch(num)
    return math.RoundToInt(39.9429*math.Pow(2.0, num/12.0))
y=input.y-h+10
x=input.x
?!isMobile
    w=screen.w
    h=screen.h
?key=primaryBegin&y>0&x<((w/28))*28&x>-1&y<8
    ?isMobile
        draw.Bg(input.x-1,input.y-1,"#FFFFFF",3,3)
    ?played+3<totaltime
        ?y<5&((x-3)%28)%4<3
            ?((x-3)%28)/4=2|((x-3)%28)/4=6
                play ui_starold2 @pitch((x/28)*12+wh[(x/4)%7])@
            :?((x-3)%28)/4>2
                play ui_starold2 @pitch((x/28)*12+bl[((x-3)/4)%7-1])@
            :
                play ui_starold2 @pitch((x/28)*12+bl[((x-3)/4)%7])@
        :
            play ui_starold2 @pitch((x/28)*12+wh[(x/4)%7])@
        played=totaltime
